plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'com.diffplug.spotless' version '6.23.1'
}

group 'net.smoofyuniverse'
version '1.1.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

gradlePlugin {
    plugins {
        AppCommonGradle {
            id = 'net.smoofyuniverse.appcommon-gradle'
            displayName = 'AppCommonGradle'
            description = 'Gradle plugin for AppCommon based applications.'
            implementationClass = 'net.smoofyuniverse.common.AppCommonPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/Yeregorix/AppCommonGradle'
    vcsUrl = 'https://github.com/Yeregorix/AppCommonGradle.git'
    tags = ['application', 'appcommon', 'javafx']
}

repositories {
    gradlePluginPortal()
}

dependencies {
    implementation 'net.smoofyuniverse:DependencyExporter:1.0.7'
    implementation 'org.openjfx:javafx-plugin:0.1.0'
    implementation 'com.google.code.gson:gson:2.10.1'
}

spotless {
    ratchetFrom 'origin/master'

    java {
        licenseHeaderFile(rootProject.file('HEADER'))
    }
}

publishing {
    publications.withType(MavenPublication).configureEach {
        pom {
            name = 'AppCommonGradle'
            description = 'Gradle plugin for AppCommon based applications.'
            url = 'https://github.com/Yeregorix/AppCommonGradle'

            scm {
                connection = 'scm:git:https://github.com/Yeregorix/AppCommonGradle.git'
                developerConnection = 'scm:git:https://github.com/Yeregorix/AppCommonGradle.git'
                url = 'https://github.com/Yeregorix/AppCommonGradle'
            }

            licenses {
                license {
                    name = 'MIT License'
                    url = 'https://opensource.org/licenses/MIT'
                }
            }

            developers {
                developer {
                    id = 'Yeregorix'
                    name = 'Hugo Dupanloup'
                    email = 'yeregorix@smoofyuniverse.net'
                }
            }
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ?
                    "https://oss.sonatype.org/content/repositories/snapshots/"
                    : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username findProperty('ossrhUsername')
                password findProperty('ossrhPassword')
            }
        }
    }
}

if (project.hasProperty('signing.keyId')) {
    apply plugin: 'signing'
    signing {
        sign publishing.publications
    }
}
